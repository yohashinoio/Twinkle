// Copyright (c) 2022 Hiramoto Ittou

// This program is based on https://github.com/githole/simple-raytracer
// Copyright (c) 2014 hole
// This software is released under the MIT License (http://kagamin.net/hole/license.txt).
// A part of this software is based on smallpt (http://www.kevinbeason.com/smallpt/) and
// released under the MIT License (http://kagamin.net/hole/smallpt-license.txt).

declare class FILE;

[[nomangle]]
declare func printf(format: ^i8, ...) -> i32;

[[nomangle]]
declare func fprintf(stream: ^FILE, format: ^i8, ...) -> i32;

[[nomangle]]
declare func fdopen(fd: i32, mode: ^i8) -> ^FILE;

[[nomangle]]
declare func fopen(filename: ^i8, mode: ^i8) -> ^FILE;

[[nomangle]]
declare func fclose(stream: ^FILE) -> i32;

[[nomangle]]
declare func sqrt(x: f64) -> f64;

[[nomangle]]
declare func pow(x: f64, y: f64) -> f64;

[[nomangle]]
declare func frexp(x: f64, exp: ^i32) -> f64;

[[nomangle]]
declare func malloc(size: u64) -> ^i8;

[[nomangle]]
declare func free(ptr: ^i8);

[[nomangle]]
declare func exit(status: i32);

class HeapArray {
  HeapArray(size: usize)
  {
    p = malloc(size);
  }

  ~HeapArray()
  {
    free(p);
  }

  func get() -> ^i8
  {
    return p;
  }

private:
  let mut p: ^i8;
}

class File {
  File(fd: i32, mode: ^i8)
  {
    fp = fdopen(fd, mode);
  }

  File(filename: ^i8, mode: ^i8)
  {
    fp = fopen(filename, mode);
  }

  ~File()
  {
    if (fp)
      fclose(fp);
  }

  func stream() -> ^FILE
  {
    return fp;
  }

private:
  let mut fp: ^FILE;
}

class Vec {
  Vec()
  {
    x = 0.;
    y = 0.;
    z = 0.;
  }

  Vec(x_: f64, y_: f64, z_: f64)
  {
    x = x_;
    y = y_;
    z = z_;
  }

  func length() -> f64
  {
    return length_squared() |> sqrt();
  }

  func add(b: &Vec) -> Vec
  {
    return Vec{x + b.x, y + b.y, z + b.z};
  }

  func sub(b: &Vec) -> Vec
  {
    return Vec{x - b.x, y - b.y, z - b.z};
  }

  func mul(b: f64) -> Vec
  {
    return Vec{x * b, y * b, z * b};
  }

  func div(b: f64) -> Vec
  {
    return Vec{x / b, y / b, z / b};
  }

  let mut x: f64;
  let mut y: f64;
  let mut z: f64;

private:
  func length_squared() -> f64
  {
    return x * x + y * y + z * z;
  }
}

typedef Color = Vec;

func cross(v1: &Vec, v2: &Vec) -> Vec
{
  return
    Vec
    { (v1.y * v2.z) - (v1.z * v2.y)
    , (v1.z * v2.x) - (v1.x * v2.z)
    , (v1.x * v2.y) - (v1.y * v2.x)};
}

class Ray {
  Ray(org_: &Vec, dir_: &Vec)
  {
    org = org_;
    dir = dir_;
  }

  let mut org: Vec;
  let mut dir: Vec;
}

func dot(v1: &Vec, v2: &Vec) -> f64
{
  return v1.x * v2.x + v1.y * v2.y + v1.z * v2.z;
}

class Sphere {
  Sphere(radius_: f64,
         position_: &Vec,
         emission_: &Color,
         color_: &Color,
         ref_type_: i32)
  {
    radius = radius_;
    position = position_;
    emission = emission_;
    color = color_;
    ref_type = ref_type_;
  }

  func intersect(ray: &Ray) -> f64
  {
    let eps = 1e-6;

    let o_p = position.sub(ref ray.org);

    let b = dot(ref o_p, ref ray.dir);
    let det = b * b - dot(ref o_p, ref o_p) + radius * radius;

    if (0.0 <= det) {
      let sqrt_det = sqrt(det);
      let t1 = b - sqrt_det;
      let t2 = b + sqrt_det;

      if (t1 > eps)
        return t1;
      else if (t2 > eps)
        return t2;
    }

    return 0.0;
  }

  let mut radius: f64;
  let mut position: Vec;
  let mut emission: Color;
  let mut color: Color;
  let mut ref_type: i32;
}

func normalize(v: &Vec) -> Vec
{
  return v.div(v.length());
}

func intersect_scene(spheres: ^Sphere,
                     spheres_size: i32,
                     ray: &Ray,
                     t: mut &f64,
                     id: mut &i32) -> bool
{
  let inf = 1e20;

  t = inf;
  id = -1;

  for (let mut i = 0; i < spheres_size; ++i) {
    let d = spheres[i].intersect(ref ray);

    if (0.0 < d && d < t) {
      t = d;
      id = i;
    }
  }

  return t < inf;
}

func max(a: f64, b: f64) -> f64
{
  if (a < b)
    return b;
  else
    return a;
}

func min(a: i32, b: i32) -> i32
{
  if (a < b)
    return a;
  else
    return b;
}

func multiply(v1: &Vec, v2: &Vec) -> Vec
{
  return Vec{v1.x * v2.x, v1.y * v2.y, v1.z * v2.z};
}

func radiance(ray: &Ray, depth: i32) -> Color
{
  let max_depth = 5;
  let background_color = Color{0.0, 0.0, 0.0};

  let DIFFUSE = 0;
  let SPECULAR = 1;
  let REFRACTION = 2;

  let light_pos = Vec{50.0, 75.0, 81.6};
  let light_color = Color{256., 256., 256.};

  let spheres = [
    Sphere{ 1e5, ref Vec{1e5 + 1., 40.8, 81.6},   ref Color{}, ref Color{0.75, 0.25, 0.25},    DIFFUSE},
    Sphere{ 1e5, ref Vec{-1e5 + 99., 40.8, 81.6}, ref Color{}, ref Color{0.25, 0.25, 0.75},    DIFFUSE},
    Sphere{ 1e5, ref Vec{50., 40.8, 1e5},         ref Color{}, ref Color{0.75, 0.75, 0.75},    DIFFUSE},
    Sphere{ 1e5, ref Vec{50., 40.8, -1e5 + 170.}, ref Color{}, ref Color{},                    DIFFUSE},
    Sphere{ 1e5, ref Vec{50., 1e5, 81.6},         ref Color{}, ref Color{0.75, 0.75, 0.75},    DIFFUSE},
    Sphere{ 1e5, ref Vec{50., -1e5 + 81.6, 81.6}, ref Color{}, ref Color{0.75, 0.75, 0.75},    DIFFUSE},
    Sphere{16.5, ref Vec{27., 16.5, 47.},         ref Color{}, ref Color{1., 1., 1.}.mul(.99), SPECULAR},
    Sphere{16.5, ref Vec{73., 16.5, 78.},         ref Color{}, ref Color{1., 1., 1.}.mul(.99), REFRACTION}
  ];

  let spheres_size = (sizeof spheres / sizeof spheres[0]) as i32;

  let mut t: f64;
  let mut id: i32;
  if (!intersect_scene(&spheres[0], spheres_size, ref ray, ref t, ref id))
    return background_color;

  let obj = ref spheres[id];

  let hitpoint = ray.org.add(ref ray.dir.mul(t));

  let normal = normalize(ref hitpoint.sub(ref obj.position));

  let mut orienting_normal: Vec;
  if (dot(ref normal, ref ray.dir) < 0.0)
    orienting_normal = normal;
  else
    orienting_normal = normal.mul(-1.0);

  if (max_depth < depth)
    return Color{};

  if (obj.ref_type == DIFFUSE) {
    let ldir = light_pos.sub(ref hitpoint);

    let len = ldir.length();

    let mut t_: f64;
    let mut id_: i32;
    intersect_scene
    ( &spheres[0]
    , spheres_size
    , ref Ray{ref hitpoint, ref ldir.div(len)}
    , ref t_
    , ref id_
    );

    if (len <= t_) {
      return obj.emission.add(
                ref multiply(ref light_color, ref obj.color)
                .mul(max(0.0, dot(ref orienting_normal, ref ldir.div(len))))
                .div(len * len)
              );
    }
    else
      return Color{};
  }
  else if (obj.ref_type == SPECULAR) {
    return obj.emission.add(
      ref multiply
      ( ref obj.color
      , ref radiance
            ( ref Ray{ref hitpoint
            , ref ray.dir.sub(ref normal.mul(2.0).mul(dot(ref normal, ref ray.dir)))}
            , depth + 1)
      )
    );
  }
  else if (obj.ref_type == REFRACTION) {
    let reflection_ray = Ray{ref hitpoint,
                             ref normal
                                 .mul(2.0)
                                 .mul(dot(ref normal, ref ray.dir))
                                 .sub(ref ray.dir)};

    let into = 0.0 < dot(ref normal, ref orienting_normal);

    let nc = 1.0;
    let nt = 1.5;
    let mut nnt: f64;
    if (into)
      nnt = nc / nt;
    else
      nnt = nt / nc;
    let ddn = dot(ref ray.dir, ref orienting_normal);
    let cos2t = 1.0 - nnt * nnt * (1.0 - ddn * ddn);

    if (cos2t < 0.0) {
      return obj.emission.add(ref multiply(ref obj.color,
                                           ref radiance(ref reflection_ray, depth + 1)));
    }

    let mut tmp: f64;
    if (into)
      tmp = 1.0;
    else
      tmp = -1.0;

    let tdir = normalize(ref ray.dir.mul(nnt)
                             .sub(ref normal.mul(tmp)
                                      .mul(ddn * nnt + sqrt(cos2t))));

    let a = nt - nc;
    let b = nt + nc;
    let r0 = (a * a) / (b * b);
    let mut c: f64;
    if (into)
      c = 1.0 - -ddn;
    else
      c = 1.0 - dot(ref tdir, ref normal);
    let re = r0 + (1.0 - r0) * pow(c, 5.0);
    let tr = 1.0 - re;

    return obj.emission.add(ref multiply(ref obj.color, ref radiance(ref reflection_ray, depth + 1)
                                         .mul(re)
                                         .add(ref radiance(ref Ray{ref hitpoint, ref tdir},
                                                           depth + 1))
                                         .mul(tr)));
  }
}

class HDRPixel {
  HDRPixel(r_: u8,
           g_: u8,
           b_: u8,
           e_: u8)
  {
    r = r_;
    g = g_;
    b = b_;
    e = e_;
  }

  HDRPixel()
  {
    r = 0 as u8;
    g = 0 as u8;
    b = 0 as u8;
    e = 0 as u8;
  }

  func get(idx: i32) -> u8
  {
    if (idx == 0)
      return r;
    if (idx == 1)
      return g;
    if (idx == 2)
      return b;
    if (idx == 3)
      return e;

    return 0 as u8;
  }

  let mut r: u8;
  let mut g: u8;
  let mut b: u8;
  let mut e: u8;
}

func get_hdr_pixel(color: &Color) -> HDRPixel
{
  let d = max(color.x, max(color.y, color.z));
  if (d <= 1e-32)
    return HDRPixel{};

  let mut e: i32;
  let m = frexp(d, &e);

  let d = m * 256.0 / d;
  return HDRPixel{
    (color.x * d) as u8,
    (color.y * d) as u8,
    (color.z * d) as u8,
    (e + 128) as u8
  };
}

declare class HDRPixelNode;

class HDRPixelNode {
  let mut pix: HDRPixel;
  let mut next: ^HDRPixelNode;
}

class HDRPixelList {
  HDRPixelList()
  {
    head = new HDRPixelNode;
    head^.next = 0 as ^HDRPixelNode;

    size = 0;
  }

  ~HDRPixelList()
  {
    while (head^.next != 0 as ^HDRPixelNode) {
      let tmp = head;
      head = head^.next;
      delete tmp;
    }
  }

  func push_back(val: &HDRPixel)
  {
    let mut tail = search_tail();

    let newnode = new HDRPixelNode;

    newnode^.pix = val;
    newnode^.next = 0 as ^HDRPixelNode;

    tail^.next = newnode;

    ++size;
  }

  func at(idx: i32) -> HDRPixel
  {
    let mut p = head^.next;

    for (let mut i = 0; i != idx; ++i)
      p = p^.next;

    return p^.pix;
  }

  func size() -> i32
  {
    return size;
  }

private:
  func search_tail() -> ^HDRPixelNode
  {
    let mut p = head;

    while (p^.next != 0 as ^HDRPixelNode)
      p = p^.next;

    return p;
  }

  let mut head: ^HDRPixelNode;

  let mut size: i32;
}

func createCamera() -> Ray
{
  let a = Vec{50.0, 52.0, 295.6};
  let b = Vec{0.0, -0.042612, -1.0};
  let c = normalize(ref b);

  return Ray{ref a, ref c};
}

func save_as_hdr(filename: ^i8, image: ^Color, width: i32, height: i32)
{
  let file = File{filename, "wb"};
  let fp = file.stream();

  if (fp) {
    let ret = 0x0a as u8;
    fprintf(fp, "#?RADIANCE%c", ret);
    fprintf(fp, "# Made with 100%% pure HDR Shop%c", ret);
    fprintf(fp, "FORMAT=32-bit_rle_rgbe%c", ret);
    fprintf(fp, "EXPOSURE=1.0000000000000%c%c", ret, ret);

    fprintf(fp, "-Y %d +X %d%c", height, width, ret);
    for (let mut i = height - 1; 0 <= i; --i) {
      let line = HDRPixelList{};

      for (let mut j = 0; j < width; ++j) {
        let tmp = image[j + i * width];
        let p = get_hdr_pixel(ref tmp);
        line.push_back(ref p);
      }

      fprintf(fp, "%c%c", 0x02, 0x02);
      fprintf(fp, "%c%c", (width >> 8) & 0xFF, width & 0xFF);

      for (let mut i = 0; i < 4; ++i) {
        for (let mut cursor = 0; cursor < width;) {
          let cursor_move = min(127, width - cursor);
          fprintf(fp, "%c", cursor_move);

          for (let mut j = cursor;  j < cursor + cursor_move; ++j)
            fprintf(fp, "%c", line.at(j).get(i));

          cursor += cursor_move;
        }
      }
    }
  }
  else {
    let stderr = File{2, "w"};
    fprintf(stderr.stream(), "Failed to open file!\n");
    exit(1);
  }
}

func main() -> i32
{
  let width = 640;
  let height = 480;

  let image_heap = HeapArray{width * height * sizeof Color{}};
  let mut image = image_heap.get() as ^Color;

  let camera = createCamera();

  let cx = Vec{width as f64 * 0.5135 / height as f64,
               0.0,
               0.0};

  let cy = normalize(ref cross(ref cx, ref camera.dir)).mul(0.5135);

  let stderr = File{2, "w"};

  for (let mut y = 0; y < height; ++y) {
    fprintf
    ( stderr.stream()
    , "Rendering %.4f%%"
    , 100.0 * y as f64 / (height - 1) as f64
    );

    for (let mut x = 0; x < width; ++x) {
      let image_idx = y * width + x;
      image[image_idx] = Color{0.0, 0.0, 0.0};

      for (let mut sy = 0; sy < 2; ++sy) {
        for (let mut sx = 0; sx < 2; ++sx) {
          let dx = sx as f64 / 2.0;
          let dy = sy as f64 / 2.0;

          let dir = cx.mul(((sx as f64 + 0.5 + dx) / 2.0 + x as f64) / width as f64 - 0.5)
                    .add(ref cy.mul(((sy as f64 + 0.5 + dy) / 2.0 + y as f64) / height as f64 - 0.5)
                             .add(ref camera.dir));

          image[image_idx]
            = image[image_idx].add(ref radiance(ref Ray{ref camera.org.add(ref dir.mul(130.0)),
                                   ref normalize(ref dir)}, 0));
        }
      }
    }

    fprintf(stderr.stream(), "\r");
  }

  fprintf(stderr.stream(), "\n");

  save_as_hdr("image.hdr", image, width, height);
}
