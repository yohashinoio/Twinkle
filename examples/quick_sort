[[nomangle]] declare func printf(fmt: *i8, ...) -> i32;

func swap(a: mut *i32, b: mut *i32)
{
  let tmp = *a;
  *a = *b;
  *b = tmp;
}

func partition(a: mut *i32, start: i32, end: i32) -> i32
{
  let pivot = a[end];
  let mut i = start - 1;

  for (let mut j = start; j <= end - 1; ++j) {
    if (a[j] <= pivot) {
      ++i;
      swap(&a[i], &a[j]);
    }
  }

  swap(&a[i + 1], &a[end]);
  return i + 1;
}

func quicksort(a: mut *i32, start: i32, end: i32)
{
  if (start < end) {
    let pivot = partition(a, start, end);
    quicksort(a, start, pivot - 1);
    quicksort(a, pivot + 1, end);
  }
}

func printarray(a: *i32, s: i32)
{
  printf("%d", a[0]);
  for (let mut i = 1; i < s; ++i)
    printf(" %d", a[i]);
  printf("\n");
}

func main() -> i32
{
  let a = [48, 10, -4, 8, 1, 0];

  let s = (sizeof a / sizeof a[0]) as i32;
  let p = &a[0];

  printarray(p, s);

  quicksort(p, 0, s - 1);

  printarray(p, s);
}
